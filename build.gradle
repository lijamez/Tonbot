group 'com.github.lijamez'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
mainClassName = 'net.tonbot.core.Main'

configurations.all {
	// check for updates every build
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

run {
    // Pass all of the system properties to the application
    systemProperties System.getProperties()

    args project.findProperty('runArgs') ?: ''
	
	// Additional JVM args can be passed via the "-DjvmArgs" argument to gradle.
	// Example: ./gradlew run -DjvmArgs="-Xmx512m -Xms256m"
	applicationDefaultJvmArgs += (System.getProperty('jvmArgs', ' ').split('\\s+') as List);

    if (System.getProperty('debug', 'false') == 'true') {
        applicationDefaultJvmArgs += [
            "-agentlib:jdwp=transport=dt_socket,server=y,address=8000,suspend=n",
            "-Dcom.sun.management.jmxremote",
            "-Dcom.sun.management.jmxremote.port=9000",
            "-Dcom.sun.management.jmxremote.authenticate=false",
            "-Dcom.sun.management.jmxremote.ssl=false"]
        
        def rmiHostname = System.getProperty('rmiHostname', '')
        if (rmiHostname != '') {
            applicationDefaultJvmArgs += ["-Djava.rmi.server.hostname=" + rmiHostname]
        }
    }
}

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
	}
}

repositories {
	mavenCentral()
	jcenter() //This prevents issues with transitive dependencies
	maven { url  "https://jitpack.io" }
}

dependencies {

	// Tonbot
	implementation 'com.github.lijamez:tonbot-common:master-SNAPSHOT'

	// Tonbot Plugins
	runtime group: 'com.github.lijamez', name: 'tonbot-plugin-ifplayer', version: 'master-SNAPSHOT'
	runtime group: 'com.github.lijamez', name: 'tonbot-plugin-tmdb', version: 'master-SNAPSHOT'
	runtime group: 'com.github.lijamez', name: 'tonbot-plugin-wolframalpha', version: 'master-SNAPSHOT'
	runtime group: 'com.github.lijamez', name: 'tonbot-plugin-decisionmaker', version: 'master-SNAPSHOT'
	runtime group: 'com.github.lijamez', name: 'tonbot-plugin-statistics', version: 'master-SNAPSHOT'
	runtime group: 'com.github.lijamez', name: 'tonbot-plugin-music', version: 'master-SNAPSHOT'
	runtime group: 'com.github.lijamez', name: 'tonbot-plugin-jokes', version: 'master-SNAPSHOT'
	runtime group: 'com.github.lijamez', name: 'tonbot-plugin-trivia', version: 'master-SNAPSHOT'

	implementation group: 'com.github.austinv11', name: 'Discord4J', version: '2.9.3'

	implementation group: 'com.google.inject', name: 'guice', version: '4.2.2'
	implementation group: 'com.google.guava', name: 'guava', version: '27.1-jre'

	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
	implementation group: 'org.apache.commons', name: 'commons-text', version: '1.2'
	implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'
	
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.3'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.3'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.3'

	// Logging
	implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'
	implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.10.0'
	implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

	compileOnly 'org.projectlombok:lombok:1.16.20'

	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
}

// Force all jackson-related modules to use a single version so we don't end up with incompatibilities.
configurations.all {
	resolutionStrategy {
		eachDependency { DependencyResolveDetails details ->
			if (details.requested.group == 'com.fasterxml.jackson.core'
				|| details.requested.group == 'com.fasterxml.jackson.datatype'
				|| details.requested.group == 'com.fasterxml.jackson.module') {
				details.useVersion "2.9.1"
			}
		}
	}
}
